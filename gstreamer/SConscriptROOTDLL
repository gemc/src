# The environment is not passed here, checking for root library
from init_env import init_environment
import os
import sys

BINROOT =  os.popen('root-config --bindir').readline()

if BINROOT:
    print("Using ROOT from root-config in: " + BINROOT)
else:
    print("Warning: root-config not found. Skipping ROOT output gstreamer plugins ")
    sys.exit(1)

env = init_environment('glibrary geant4 clhep root')

# standard pre and suffix for gemc plugins and path
env.Replace(SHLIBSUFFIX = ".gplugin")
env.Replace(SHLIBPREFIX = "")
plugins_dir_s   = os.environ.get('GPLUGIN_PATH')
plugins_dir     = os.path.realpath(plugins_dir_s)
plugin_output   =  plugins_dir  + '/' + 'gstreamerROOTFactory'

env.Append(CXXFLAGS=['-std=c++20'])
env.Append(CPPPATH = [Dir('factories/ROOT')])

# needed for the plugin so we can use install_name_tool to change path for DMG installation
env.Append(SHLINKFLAGS=['-headerpad_max_install_names'])

plugin_sources = [
					'factories/ROOT/gRootTree.cc',
					'factories/ROOT/gstreamerROOTFactory.cc',
					'factories/ROOT/gstreamerROOTConnection.cc',
					'factories/ROOT/event/event.cc',
					'factories/ROOT/event/eventHeader.cc',
					'factories/ROOT/event/publishTrueInfo.cc',
					'factories/ROOT/event/publishDigitized.cc',
					'factories/ROOT/stream/stream.cc',
					'factories/ROOT/stream/frameHeader.cc',
					'factories/ROOT/stream/publishPayload.cc'
					]

# notice: these two libraries needs to be added explicitly, with also the dependency below,
# as the env scanner may run at a time where the libraries are not built yet
env.Append(LIBS = ['gdata', 'guts'])

# destination library is one dir up - relative to this SConscript
root_dll = env.SharedLibrary(source = plugin_sources, target = plugin_output)
Requires(root_dll, '../lib/libgdata.a')
Requires(root_dll, '../lib/libtextProgressBar.a')
Requires(root_dll, '../lib/libgsystem.a')
